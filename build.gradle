// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    dependencies {
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.4'
    }
}

plugins {
    id 'com.android.application' version '7.4.1' apply false
    id 'com.android.library' version '7.4.1' apply false
    id 'net.ltgt.errorprone' version '2.0.2' apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    compileSdkVersion = 33
    buildToolsVersion = '33.0.0'
    targetSdkVersion = 33
    minSdkVersion = 19
    errorProneVersion = '2.15.0'
    buildUsesGoogle = {
        return getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains('google')
    }
}

allprojects {
    configurations.all {
        exclude module: 'commons-logging'
    }
    afterEvaluate {
        tasks.withType(JavaCompile).configureEach { task ->
            options.errorprone {
                enabled = true
                disableWarningsInGeneratedCode = true
                disable('JdkObsolete')
                disable('MissingSummary')
                disable('EmptyBlockTag')
                disable('UnnecessaryParentheses')
                disable('JavaUtilDate')
            }
        }
    }
}
