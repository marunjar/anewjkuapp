// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath 'com.google.gms:google-services:4.3.4'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('net.sf.proguard:proguard-gradle') with module('com.guardsquare:proguard-gradle:7.0.1')
            }
        }
    }
}

plugins {
    id "net.ltgt.errorprone" version "1.3.0"
}

ext {
    compileSdkVersion = 30
    buildToolsVersion = "30.0.3"
    targetSdkVersion = 30
    minSdkVersion = 16
    errorProneVersion = '2.4.0'
    errorProneJavacVersion = '9+181-r4173-1'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    configurations.all {
        exclude module: 'commons-logging'
    }
    afterEvaluate {
        tasks.withType(JavaCompile).configureEach {
            options.errorprone {
                disable('JdkObsolete')
                disable('MissingSummary')
                disable('EmptyBlockTag')
            }
        }
    }
}
