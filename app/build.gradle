plugins {
    id 'com.android.application'
    id 'net.ltgt.errorprone'
}

if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains('google')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    logger.warn('google stuff applied')
} else {
    logger.warn('google stuff not applied')
}

android {
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    signingConfigs {
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'org.voidsink.anewjkuapp'
        versionCode 140086
        versionName '0.54.0'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true // Android Plugin for Gradle 2.0.0

        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                'proguard-rules.txt',
                'proguard-rules-ical4j.txt',
                'proguard-rules-keep-names.txt'

        buildConfigField 'boolean', 'FOSS_ONLY', 'false'

        archivesBaseName = "${applicationId}-${versionName}-${versionCode}"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false

            applicationIdSuffix ''
            versionNameSuffix ''
        }
        debug {
            minifyEnabled true
            shrinkResources true
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }

    flavorDimensions 'tier'

    productFlavors {
        google {
            dimension 'tier'
            versionNameSuffix '-google'

//            firebaseCrashlytics {
//                mappingFileUploadEnabled false
//            }
        }
        fdroid {
            dimension 'tier'
            buildConfigField 'boolean', 'FOSS_ONLY', 'true'
            versionNameSuffix '-fdroid'
        }
    }

    lint {
        checkAllWarnings true
        abortOnError false
        lintConfig file('lint.xml')
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:${rootProject.ext.errorProneVersion}")
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.work:work-runtime:2.7.1'
    googleImplementation 'androidx.work:work-gcm:2.7.1'
    googleImplementation 'com.google.firebase:firebase-analytics:19.0.2'
    googleImplementation 'com.google.firebase:firebase-crashlytics:18.2.8'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    implementation 'org.mapsforge:mapsforge-core:0.17.0'
    implementation 'org.mapsforge:mapsforge-map:0.17.0'
    implementation 'org.mapsforge:mapsforge-themes:0.17.0'
    implementation 'org.mapsforge:mapsforge-map-android:0.17.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'org.jsoup:jsoup:1.13.1'
    //breaks when upgrading ical4j > 3.0.21
    //noinspection GradleDependency
    implementation 'org.mnode.ical4j:ical4j:3.0.21'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    //breaks when upgrading commons.lang3 > 3.9
    //noinspection GradleDependency
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.slf4j:slf4j-android:1.7.36'
    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.loader:loader:1.1.0'
    implementation 'androidx.activity:activity:1.4.0'
    implementation 'androidx.fragment:fragment:1.4.1'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation project(':ckChangeLog')
    implementation project(':sectionedRecyclerAdapter')
    implementation project(':android-week-view')
    implementation 'com.android.support:multidex:1.0.3'
}
