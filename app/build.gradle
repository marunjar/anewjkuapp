apply plugin: 'com.android.application'
apply plugin: 'net.ltgt.errorprone'
if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
} else {
    logger.warn('google stuff not applied')
}

android {
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    signingConfigs {
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'org.voidsink.anewjkuapp'
        versionCode 140084
        versionName '0.53.5'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        vectorDrawables.useSupportLibrary = true // Android Plugin for Gradle 2.0.0

        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                'proguard-rules.txt',
                'proguard-rules-ical4j.txt',
                'proguard-rules-keep-names.txt',
                'proguard-rules-streamsupport.pro'

        buildConfigField 'boolean', 'FOSS_ONLY', 'false'

        archivesBaseName = "${applicationId}-${versionName}-${versionCode}"

        return void // fix lint warning 'Not all execution paths return a value'
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false

            applicationIdSuffix ''
            versionNameSuffix ''

            return void // fix lint warning 'Not all execution paths return a value'
        }
        debug {
            minifyEnabled true
            shrinkResources true
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            return void // fix lint warning 'Not all execution paths return a value'
        }
    }

    flavorDimensions 'tier'

    productFlavors {
        google {
            dimension 'tier'
            versionNameSuffix '-google'

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }

            return void // fix lint warning 'Not all execution paths return a value'
        }
        fdroid {
            dimension 'tier'
            buildConfigField 'boolean', 'FOSS_ONLY', 'true'
            versionNameSuffix '-fdroid'
            return void // fix lint warning 'Not all execution paths return a value'
        }
    }

    lintOptions {
        checkAllWarnings true
        abortOnError false
        lintConfig file('lint.xml')
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'

        return void // fix lint warning 'Not all execution paths return a value'
    }
}

dependencies {
    errorproneJavac("com.google.errorprone:javac:${rootProject.ext.errorProneJavacVersion}")
    errorprone("com.google.errorprone:error_prone_core:${rootProject.ext.errorProneVersion}")
    implementation 'net.sourceforge.streamsupport:streamsupport:1.7.3'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.work:work-runtime:2.5.0'
    googleImplementation 'androidx.work:work-gcm:2.5.0'
    googleImplementation 'com.google.firebase:firebase-analytics:18.0.2'
    googleImplementation 'com.google.firebase:firebase-crashlytics:17.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    implementation 'org.mapsforge:mapsforge-core:0.15.0'
    implementation 'org.mapsforge:mapsforge-map:0.15.0'
    implementation 'org.mapsforge:mapsforge-themes:0.15.0'
    implementation 'org.mapsforge:mapsforge-map-android:0.15.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.mnode.ical4j:ical4j:3.0.21'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    //breaks when upgrading commons.lang3 > 3.9
    //noinspection GradleDependency
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.slf4j:slf4j-android:1.7.30'
    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.loader:loader:1.1.0'
    implementation 'androidx.activity:activity:1.2.0'
    implementation 'androidx.fragment:fragment:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation project(':ckChangeLog')
    implementation project(':sectionedRecyclerAdapter')
    implementation project(':android-week-view')
}
